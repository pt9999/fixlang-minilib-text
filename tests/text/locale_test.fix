module LocaleTest;

import Minilib.Text.Locale;
import Minilib.Testing.UnitTest;

test_init_locale: TestCase;
test_init_locale = (
    make_test("test_init_locale") $ |_|

    // At the very first, LC_ALL=C in most cases, but this is platform dependent
    let ret_get_initial = *get_locale(lc_all);

    //  equal to `set_local(lc_all, locale_default)`
    init_locale.lift;;
    let ret_get = *get_locale(lc_all);            // maybe the value of ${LANG}, platform dependent
    let ret_set = *set_locale(lc_all, locale_default);
    assert_equal("eq", ret_set, ret_get);;          // init_locale is equal to set_locale(LC_ALL, locale_default)
    pure()
);

test_set_locale: TestCase;
test_set_locale = (
    make_test("test_set_locale") $ |_|
    let ret = *set_locale(lc_all, locale_default);
    //assert_equal("eq", "ja_JP.UTF-8", ret);;    // platform dependent
    assert_true("LC_ALL,default", ret != locale_default);;
    let ret = *set_locale(lc_all, locale_C);
    assert_equal("LC_ALL,C", locale_C, ret);;
    let ret = *set_locale(lc_all, locale_C_UTF8);
    assert_equal("LC_ALL,C", locale_C_UTF8, ret);;
    /*
    // The following tests do not always succeed (platform dependent)
    let ret = *set_locale(lc_all, "en_US.UTF-8");
    assert_equal("LC_ALL,en_US.UTF-8", "en_US.UTF-8", ret);;
    */

    let res = *set_locale(lc_all, "invalid_value").to_result.lift; // ENOENT
    assert_true("LC_ALL,invalid_value", res.is_err);;

    let res = *set_locale("LC_INVALID_CATEGORY", locale_C).to_result.lift; // EINVAL
    assert_true("LC_INVALID_CATEGORY,C", res.is_err);;
    pure()
);

test_get_locale: TestCase;
test_get_locale = (
    make_test("test_get_locale") $ |_|

    // set all categories to default value (platform dependent)
    let ret_set = *set_locale(lc_all, locale_default);
    let ret_get = *get_locale(lc_all);
    assert_equal("setlocale(LC_ALL,\"\")", ret_set, ret_get);;

    // set all categories to locale_C
    let ret_set = *set_locale(lc_all, locale_C);
    assert_equal("setlocale(LC_ALL,C)", locale_C, ret_set);;
    let ret_get = *get_locale(lc_all);
    assert_equal("getlocale(LC_ALL) after setlocale(LC_ALL,C)", locale_C, ret_get);;

    // set LC_TIME=C
    set_locale(lc_time, locale_C);;
    let ret_get = *get_locale(lc_time);
    assert_equal("getlocale(LC_TIME) after setlocale(LC_TIME,C)", locale_C, ret_get);;
    let ret_get = *get_locale(lc_all);
    assert_equal("getlocale(LC_ALL) after setlocale(LC_TIME,C)", locale_C, ret_get);;

    // set LC_TIME=C.UTF-8
    set_locale(lc_time, locale_C_UTF8);;
    let ret_get = *get_locale(lc_time);
    assert_equal("getlocale(LC_TIME) after setlocale(LC_TIME,C.UTF-8)", locale_C_UTF8, ret_get);;
    let ret_get = *get_locale(lc_all);
    assert_true("getlocale(LC_ALL) after setlocale(LC_TIME,C.UTF-8)", locale_C != ret_get);;
    assert_true("getlocale(LC_ALL) after setlocale(LC_TIME,C.UTF-8)", locale_C_UTF8 != ret_get);;

    pure()
);

test_with_locale: TestCase;
test_with_locale = (
    make_test("test_with_locale") $ |_|
    let original_lc_all = *get_locale(lc_all);
    with_locale(lc_all, locale_C_UTF8, do {
        assert_equal("after LC_ALL=C.UTF-8", locale_C_UTF8, *get_locale(lc_all));;
        with_locale(lc_time, locale_C, do {
            assert_equal("after LC_TIME=C", locale_C, *get_locale(lc_time))
        });;
        assert_equal("after restoring LC_TIME=C", locale_C_UTF8, *get_locale(lc_time))
    });;
    assert_equal("original_lc_all", original_lc_all, *get_locale(lc_all))
);

main: IO ();
main = (
    [
        test_init_locale,
        test_set_locale,
        test_get_locale,
        test_with_locale,
    ].run_test_driver;;

    init_locale     // restore the locale
);
